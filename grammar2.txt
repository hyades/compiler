PROGRAM -> OTHERFUNCTIONS MAINFUNCTION  .
MAINFUNCTION -> tk_main STMTS tk_end  .
OTHERFUNCTIONS -> FUNCTION OTHERFUNCTIONS  .
OTHERFUNCTIONS ->   .
FUNCTION -> tk_funid INPUT_PAR OUTPUT_PAR tk_sem STMTS tk_end  .
INPUT_PAR -> tk_input tk_parameter tk_list tk_sql PARAMETER_LIST tk_sqr  .
OUTPUT_PAR -> tk_output tk_parameter tk_list tk_sql PARAMETER_LIST tk_sqr  .
OUTPUT_PAR ->   .
PARAMETER_LIST -> DATATYPE tk_id REMAINING_LIST  .
DATATYPE -> PRIMITIVEDATATYPE  .
DATATYPE -> CONSTRUCTEDDATATYPE  .
PRIMITIVEDATATYPE -> tk_int  .
PRIMITIVEDATATYPE -> tk_real  .
CONSTRUCTEDDATATYPE -> tk_record tk_recordid  .
REMAINING_LIST -> tk_comma PARAMETER_LIST  .
REMAINING_LIST ->   .
STMTS -> TYPEDEFINITIONS DECLARATIONS OTHERSTMTS RETURNSTMT  .
TYPEDEFINITIONS -> TYPEDEFINITION TYPEDEFINITIONS  .
TYPEDEFINITIONS ->   .
TYPEDEFINITION -> tk_record tk_recordid FIELDDEFINITIONS tk_endrecord tk_sem  .
FIELDDEFINITIONS -> FIELDDEFINITION FIELDDEFINITION MOREFIELDS  .
FIELDDEFINITION -> tk_type PRIMITIVEDATATYPE tk_colon tk_fieldid tk_sem  .
MOREFIELDS -> FIELDDEFINITION MOREFIELDS  .
MOREFIELDS ->   .
DECLARATIONS -> DECLARATION DECLARATIONS  .
DECLARATIONS ->   .
DECLARATION -> tk_type DATATYPE tk_colon tk_id GLOBAL_OR_NOT tk_sem  .
GLOBAL_OR_NOT -> tk_colon tk_global  .
GLOBAL_OR_NOT ->   .
OTHERSTMTS -> STMT OTHERSTMTS  .
OTHERSTMTS ->   .
STMT -> ASSIGNMENTSTMT  .
STMT -> ITERATIVESTMT  .
STMT -> CONDITIONALSTMT  .
STMT -> IOSTMT  .
STMT -> FUNCALLSTMT  .
ASSIGNMENTSTMT -> SINGLEORRECID tk_assignop ARITHMETICEXPRESSION tk_sem  .
SINGLEORRECID -> tk_id  .
SINGLEORRECID -> tk_recordid tk_dot tk_fieldid  .
FUNCALLSTMT -> OUTPUTPARAMETERS tk_call tk_funid tk_with tk_parameters INPUTPARAMETERS  .
OUTPUTPARAMETERS -> tk_sql IDLIST tk_sqr tk_assignop  .
OUTPUTPARAMETERS ->   .
INPUTPARAMETERS -> tk_sql IDLIST tk_sqr  .
ITERATIVESTMT -> tk_while tk_op BOOLEANEXPRESSION tk_cl STMT OTHERSTMTS tk_endwhile  .
CONDITIONALSTMT -> tk_if tk_op BOOLEANEXPRESSION tk_cl tk_then STMT OTHERSTMTS ELSEPART  .
ELSEPART -> tk_else STMT OTHERSTMTS tk_endif  .
ELSEPART -> tk_endif  .
IOSTMT -> tk_read tk_op SINGLEORRECID tk_cl tk_sem  .
IOSTMT -> tk_write tk_op ALLVAR tk_cl tk_sem  .
ALLVAR -> VAR  .
ALLVAR -> tk_recordid  .
ARITHMETICEXPRESSION -> TERM EXPPRIME  .
EXPPRIME -> LOWPRECEDENCEOPERATORS TERM EXPPRIME  .
EXPPRIME ->   .
TERM -> FACTOR TERMPRIME  .
TERMPRIME -> HIGHPRECEDENCEOPERATORS FACTOR TERMPRIME  .
TERMPRIME ->   .
FACTOR -> tk_op ARITHMETICEXPRESSION tk_cl  .
FACTOR -> ALL  .
HIGHPRECEDENCEOPERATORS -> tk_mul  .
HIGHPRECEDENCEOPERATORS -> tk_div  .
LOWPRECEDENCEOPERATORS -> tk_plus  .
LOWPRECEDENCEOPERATORS -> tk_minus  .
ALL -> tk_id  .
ALL -> tk_num  .
ALL -> tk_rnum  .
ALL -> tk_recordid TEMP  .
TEMP ->   .
TEMP -> tk_dot tk_field  .
BOOLEANEXPRESSION -> tk_op BOOLEANEXPRESSION tk_cl LOGICALOP tk_op BOOLEANEXPRESSION tk_cl  .
BOOLEANEXPRESSION -> VAR RELATIONALOP VAR  .
BOOLEANEXPRESSION -> tk_not BOOLEANEXPRESSION  .
VAR -> tk_id  .
VAR -> tk_num  .
VAR -> tk_rnum  .
LOGICALOP -> tk_and  .
LOGICALOP -> tk_or  .
RELATIONALOP -> tk_lt  .
RELATIONALOP -> tk_le  .
RELATIONALOP -> tk_eq  .
RELATIONALOP -> tk_gt  .
RELATIONALOP -> tk_ge  .
RELATIONALOP -> tk_ne  .
RETURNSTMT -> tk_return OPTIONALRETURN tk_sem  .
OPTIONALRETURN -> tk_sql IDLIST tk_sqr  .
OPTIONALRETURN ->   .
IDLIST -> tk_id MORE_IDS  .
MORE_IDS -> tk_comma IDLIST  .
MORE_IDS ->   .
